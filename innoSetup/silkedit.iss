; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SilkEdit"
#define MyAppExeName "silkedit.exe"
#define ReleaseDir "..\build\Release"
#define MyAppVersion GetStringFileInfo("{#ReleaseDir}\silkedit.exe", PRODUCT_VERSION)
#define MyAppPublisher "SilkEdit"
#define MyAppURL ""
#define Arch "x64"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8E81584D-09FD-4106-B1BE-E9976CF90AE9}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=SilkEdit_Setup_{#Arch}
OutputDir={#ReleaseDir}
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
ShowLanguageDialog=no

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "ja"; MessagesFile: "compiler:languages\Japanese.isl"

[CustomMessages]
en.OpenWithSilkEdit=Open with {#MyAppName}
en.AddContextMenu=Add "Open with {#MyAppName}" &context menu
ja.OpenWithSilkEdit={#MyAppName}で開く
ja.AddContextMenu="{#MyAppName}で開く"コンテキストメニューを追加する(&C)

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; Flags: unchecked
Name: "contextmenu"; Description: "{cm:AddContextMenu}"; Flags: unchecked

[Files]
Source: "{#ReleaseDir}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs

[Icons]
Name: "{userprograms}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Registry]
Root: HKCR; Subkey: "*\shell\{cm:OpenWithSilkEdit}"; Flags: uninsdeletekey; Tasks: contextmenu
Root: HKCR; Subkey: "*\shell\{cm:OpenWithSilkEdit}\command"; ValueType: string; ValueData: "{app}\{#MyAppExeName} ""%1"""; Tasks: contextmenu